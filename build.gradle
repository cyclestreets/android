buildscript {
  ext.kotlinVersion = '1.4.10'
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:4.0.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

ext {
  // From e.g. https://source.android.com/setup/start/build-numbers
  // Android 10          29  Q
  // Android 9.0         28  PIE
  // Android 8.1         27  OREO_MR1
  // Android 8.0         26  OREO
  // Android 7.1.1, 7.1  25  NOUGAT_MR1
  // Android 7.0         24  NOUGAT
  // Android 6.0         23  MARSHMALLOW
  // Android 5.1         22  LOLLIPOP_MR1
  // Android 5.0         21  LOLLIPOP
  // Android 4.4W        20  KITKAT_WATCH
  // Android 4.4         19  KITKAT
  // Android 4.3         18  JELLY_BEAN_MR2
  // Android 4.2, 4.2.2  17  JELLY_BEAN_MR1

  // Remember to keep these values in sync with .travis.yml
  compileSdkVersion=30
  minSdkVersion=23
  targetSdkVersion=30
  buildToolsVersion='30.0.2'

  // centrally manage some other dependencies
  mapsforgeVersion = '0.13.0' // TODO: 0.14.0 is available
  osmdroidVersion = '6.1.6'   // TODO: 6.1.8 is available - test and remove our own implementation of https://github.com/osmdroid/osmdroid/issues/1578

  // test dependencies
  assertjVersion = '3.17.2'
  junitVersion = '4.13'
  mockitoVersion = '2.28.2'
  robolectricVersion = '4.4'
}

// Configure common settings in all projects
subprojects {
  if (!file("${projectDir}/build.gradle").exists())
    return

  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
  }

  buildscript {
    repositories {
      google()
      jcenter()
    }
  }

  repositories {
    google()
    jcenter()
    maven { url "https://jitpack.io" }
  }

  if (projectDir.getName().equals('cyclestreets.app')) {
    apply plugin: 'com.android.application'
  } else {
    apply plugin: 'com.android.library'
  }

  apply plugin: 'kotlin-android'

  android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
      minSdkVersion rootProject.ext.minSdkVersion
      targetSdkVersion rootProject.ext.targetSdkVersion
    }

    lintOptions {
      // Required due to strictness of the Android developer tools.  See lint.xml for full details.
      lintConfig rootProject.file('gradle/lint.xml')

      // Kotlin (nullability) interop check - potentially useful but quite noisy
      // check 'Interoperability'
    }

    buildFeatures.viewBinding = true
  }
}
