buildscript {
  ext.kotlinVersion = '1.4.31'
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:4.1.3'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

ext {
  // From e.g. https://source.android.com/setup/start/build-numbers
  // Android 11          30  R
  // Android 10          29  Q
  // Android 9.0         28  PIE
  // Android 8.1         27  OREO_MR1
  // Android 8.0         26  OREO
  // Android 7.1.1, 7.1  25  NOUGAT_MR1
  // Android 7.0         24  NOUGAT
  // Android 6.0         23  MARSHMALLOW
  // Android 5.1         22  LOLLIPOP_MR1
  // Android 5.0         21  LOLLIPOP
  // Android 4.4W        20  KITKAT_WATCH
  // Android 4.4         19  KITKAT
  // Android 4.3         18  JELLY_BEAN_MR2
  // Android 4.2, 4.2.2  17  JELLY_BEAN_MR1

  // Remember to keep these values in sync with .travis.yml
  compileSdkVersion=30
  minSdkVersion=23
  targetSdkVersion=30
  buildToolsVersion='30.0.3'

  // java version
  javaVersion = JavaVersion.VERSION_1_8

  // centrally manage some other dependencies
  mapsforgeVersion = '0.13.0' // TODO: 0.14.0 is available
  osmdroidVersion = '6.1.6'   // TODO: 6.1.8 is available - test and remove our own implementation of https://github.com/osmdroid/osmdroid/issues/1578

  // test dependencies
  assertjVersion = '3.19.0'
  junitVersion = '4.13.2'
  mockitoVersion = '3.8.0'
  robolectricVersion = '4.5.1'
}

// Configure common settings in all projects
subprojects {
  if (!file("${projectDir}/build.gradle").exists())
    return

  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
    tasks.withType(Test) {
      testLogging {
        exceptionFormat 'full'
        showCauses true
        showExceptions true
        showStackTraces true
        events = ['passed', 'failed', 'skipped']
      }
    }
  }

  buildscript {
    repositories {
      mavenCentral()
      google() // required for Android plugins
    }
  }

  repositories {
    mavenCentral()
    google() // required for Android libraries
    maven { url "https://jitpack.io" } // required for https://github.com/smart-fun/XmlToJson
    jcenter {
      content {
        // JCenter goes down on 1st Feb 2022, see https://www.infoq.com/news/2021/02/jfrog-jcenter-bintray-closure/
        // we still need it for this artifact as per https://youtrack.jetbrains.com/issue/IDEA-261387
        includeModule("org.jetbrains.trove4j", "trove4j")
      }
    }
  }

  if (projectDir.getName().equals('cyclestreets.app')) {
    apply plugin: 'com.android.application'
  } else {
    apply plugin: 'com.android.library'
  }

  apply plugin: 'kotlin-android'

  android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
      sourceCompatibility rootProject.ext.javaVersion
      targetCompatibility rootProject.ext.javaVersion
    }

    defaultConfig {
      minSdkVersion rootProject.ext.minSdkVersion
      targetSdkVersion rootProject.ext.targetSdkVersion
    }

    lintOptions {
      // Required due to strictness of the Android developer tools.  See lint.xml for full details.
      lintConfig rootProject.file('gradle/lint.xml')

      // Kotlin (nullability) interop check - potentially useful but quite noisy
      // check 'Interoperability'
    }

    buildFeatures.viewBinding = true
  }
}
