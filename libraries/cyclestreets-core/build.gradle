configurations {
  compile.exclude module: 'commons-logging'
  compile.exclude module: 'httpclient'
}

dependencies {
  compile 'org.osmdroid:osmdroid-android:5.2'
  compile 'org.slf4j:slf4j-android:1.7.21'

  // Retrofit for HTTP Client activities
  compile 'com.squareup.retrofit2:retrofit:2.1.0'
  compile 'com.squareup.retrofit2:converter-scalars:2.1.0'
  compile('com.squareup.retrofit2:converter-simplexml:2.1.0') {
    // Exclusions required as per https://github.com/square/retrofit/issues/1796
    exclude group: 'xpp3', module: 'xpp3'
    exclude group: 'stax', module: 'stax-api'
    exclude group: 'stax', module: 'stax'
  }
  compile 'com.squareup.retrofit2:converter-jackson:2.1.0'
  compile 'de.grundid.opendatalab:geojson-jackson:1.6'

  // Jackson is already included transitively from multiple sources; we specify explicit versions
  // to ensure consistency across these artifacts.
  compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.7'
  compile 'com.fasterxml.jackson.core:jackson-core:2.7.7'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.7.7'

  testCompile 'com.github.tomakehurst:wiremock-standalone:2.1.12'
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile 'org.robolectric:robolectric:3.3.2'
  testCompile 'commons-io:commons-io:2.4'
}

def buildNumber = {
  System.getenv("BUILD_IDENTIFIER") ?: "local-build"
}()

android {
  defaultConfig {
    buildConfigField 'String', 'BUILD_IDENTIFIER', "\"${buildNumber}\""
  }
}
